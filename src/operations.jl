#=
operations.jl; This file implements operations that can be done on hypervectors to enable them to encode text-based data.
=#

#=

| Operation            | symbol | remark                                                                                                          |
| -------------------- | ------ | --------------------------------------------------------------------------------------------------------------- |
| Bundling/aggregating | `+`    | combines the information of two vectors into a new vector similar to both                                       |
| Binding              | `*`    | mapping, combines the two vectors in something different from both, preserves distance, distributes of bundling |
| Shifting             | `ρ`    | Permutation (in practice cyclic shifting), distributes over addition, conserves distance                        |
=#

"""
    grad2bipol(x::Number)

Maps a graded number in [0, 1] to the [-1, 1] interval.
"""
grad2bipol(x::Real) = 2x - one(x)


"""
bipol2grad(x::Number)

Maps a bipolar number in [-1, 1] to the [0, 1] interval.
"""
bipol2grad(x::Real) = (x + one(x)) / 2

three_pi(x, y) = abs(x - y) == 1 ? zero(x) : x * y / (x * y + (one(x) - x) * (one(y) - y))
fuzzy_xor(x, y) = (one(x) - x) * y + x * (one(y) - y)

three_pi_bipol(x, y) = grad2bipol(three_pi(bipol2grad(x), bipol2grad(y)))
fuzzy_xor_bipol(x, y) = grad2bipol(fuzzy_xor(bipol2grad(x), bipol2grad(y)))  # currently just *

aggfun(::Type{<:AbstractHV}) = +
aggfun(::GradedHV) = three_pi
aggfun(::GradedBipolarHV) = three_pi_bipol

bindfun(::AbstractHV) = *
bindfun(::BinaryHV) = ⊻
bindfun(::GradedHV) = fuzzy_xor
bindfun(::GradedBipolarHV) = fuzzy_xor_bipol

neutralbind(hdv::AbstractHV) = one(eltype(hdv))
neutralbind(hdv::BinaryHV) = false
neutralbind(hdv::GradedHV) = zero(eltype(hdv))
neutralbind(hdv::GradedBipolarHV) = -one(eltype(hdv))

noisy_and(a, b) = a == b ? a : rand(Bool)

function elementreduce!(f, itr, init)
    return foldl(itr; init) do acc, value
        acc .= f.(acc, value)
    end
end

# computes `r[i] = f(x[i], y[i+offset])`
# assumes postive offset (for now)
@inline function offsetcombine!(r, f, x, y, offset = 0)
    @assert length(r) == length(x) == length(y)
    n = length(r)
    if offset == 0
        r .= f.(x, y)
    else
        i′ = n - offset
        for i in 1:n
            i′ = i′ == n ? 1 : i′ + 1
            @inbounds r[i] = f(x[i], y[i′])
        end
    end
    return r
end

@inline function offsetcombine(f, x::V, y::V, offset = 0) where {V <: AbstractVecOrMat}
    @assert length(x) == length(y)
    r = similar(x)
    n = length(r)
    if offset == 0
        r .= f.(x, y)
    else
        i′ = n - offset
        for i in 1:n
            i′ = i′ == n ? 1 : i′ + 1
            @inbounds r[i] = f(x[i], y[i′])
        end
    end
    return r
end

# BUNDLE
# ------

# binary and bipolar: use majority
#
# When an even number of hypervectors is bundled, positions with an equal number
# of votes for each state are ties that have to be resolved. To keep bundling
# deterministic (identical inputs always yield the same result) while avoiding
# the positional bias a fixed per-index rule would introduce, ties are broken
# with a local RNG seeded from the aggregated votes, which depend on every
# input hypervector. Pass `rng` to override the seed (e.g. reproducible draws
# from a caller-controlled stream), or `rng = Random.default_rng()` to recover
# the previous, non-deterministic behaviour.
#
# The vote counts are kept bit-sliced over the BitVector storage: `planes[j, i]`
# holds bit `j` of the counters for the 64 positions in chunk `i`, so a single
# machine word tracks 64 counters at once. Adding a word of votes is a carry
# ripple through its planes (amortized O(1) planes touched, like a binary
# counter increment); inputs are pre-combined in triples with a bitwise full
# adder (Harley–Seal style) so only two ripples are needed per three inputs.
# The majority vote `count > m ÷ 2` is a bit-parallel magnitude comparator over
# the planes — no per-element integer accumulator ever exists.

# add a word `c` of weight `2^(j-1)` votes into the sliced counters of chunk `i`;
# never ripples past the top plane as long as the total count stays below 2^nplanes
@inline function ripple_votes!(planes, i, c, j)
    @inbounds while c != 0
        p = planes[j, i]
        planes[j, i] = p ⊻ c
        c &= p
        j += 1
    end
    return nothing
end

function bundle(hvr::Union{BinaryHV, BipolarHV}, hdvs, r = nothing; rng::Union{Nothing, AbstractRNG} = nothing)
    m = length(hdvs)
    n = length(hvr)
    nc = length(hvr.v.chunks)
    nplanes = 8 * sizeof(m) - leading_zeros(m)  # bits needed for counts up to m
    planes = zeros(UInt64, nplanes, nc)
    cvs = Vector{Vector{UInt64}}(undef, m)
    for (k, hv) in enumerate(hdvs)
        chunks = (hv.v::BitVector).chunks
        length(chunks) == nc || throw(DimensionMismatch("hypervectors must have equal length"))
        cvs[k] = chunks
    end
    k = 1
    @inbounds while k + 2 <= m
        ca, cb, cc = cvs[k], cvs[k + 1], cvs[k + 2]
        for i in 1:nc
            a, b, c = ca[i], cb[i], cc[i]
            axb = a ⊻ b
            ripple_votes!(planes, i, axb ⊻ c, 1)             # sum bits, weight 1
            ripple_votes!(planes, i, (a & b) | (c & axb), 2)  # carry bits, weight 2
        end
        k += 3
    end
    @inbounds while k <= m
        ck = cvs[k]
        for i in 1:nc
            ripple_votes!(planes, i, ck[i], 1)
        end
        k += 1
    end
    t = m >>> 1  # count > t is a majority; count == t (m even) is a tie
    even = iseven(m)
    tie_rng = if !even
        nothing
    elseif isnothing(rng)
        seed = hash(m)
        for x in planes
            seed = hash(x, seed)
        end
        Xoshiro(seed)
    else
        rng
    end
    res = BitVector(undef, n)
    rchunks = res.chunks
    msk_end = n & 63 == 0 ? ~UInt64(0) : (UInt64(1) << (n & 63)) - UInt64(1)
    @inbounds for i in 1:nc
        # bit-parallel comparison of the 64 sliced counters in chunk i against t,
        # scanning count bits from most to least significant
        gt = UInt64(0)   # positions where count > t is already decided
        eq = ~UInt64(0)  # positions where count == t so far
        for j in nplanes:-1:1
            p = planes[j, i]
            if (t >>> (j - 1)) & 1 == 1
                eq &= p
            else
                gt |= eq & p
                eq &= ~p
            end
        end
        w = gt
        even && (w |= eq & rand(tie_rng, UInt64))
        i == nc && (w &= msk_end)
        rchunks[i] = w
    end
    return typeof(hvr)(res)
end

# ternary: just add them, no normalization by default
function bundle(
        ::TernaryHV, hdvs, r;
        normalize = false
    )
    for hv in hdvs
        r .+= hv.v
    end
    normalize && clamp!(r, -1, 1)
    # inner constructor: bundling may exceed the ternary domain by design
    return TernaryHV{eltype(r)}(r)
end

# realhv: just add + rescale with sqrt m
function bundle(hv1::RealHV, hdvs, r)
    m = 0
    for hv in hdvs
        r .+= hv.v
        m += 1
    end
    r ./= sqrt(m)
    return RealHV(r, hv1.distr)
end

function bundle(hv1::GradedHV, hdvs, r)
    for hv in hdvs
        r .= three_pi.(r, hv.v)
    end
    return GradedHV(r, hv1.distr)
end

function bundle(hv1::GradedBipolarHV, hdvs, r)
    for hv in hdvs
        r .= three_pi_bipol.(r, hv.v)
    end
    return GradedBipolarHV(r, hv1.distr)
end

function bundle(::FHRR, hdvs, r)
    for hv in hdvs
        r .+= hv.v
    end
    r ./= abs.(r)
    return FHRR(r)
end

"""
    bundle(hvs; kwargs...)

Bundle (superpose) a collection of hypervectors into a single hypervector that is
similar to every input. Overloaded as the `+` operator.

The aggregation rule depends on the hypervector type: majority vote with
deterministic tie-breaking ([`BinaryHV`](@ref), [`BipolarHV`](@ref)), elementwise
addition ([`TernaryHV`](@ref), [`RealHV`](@ref)), fuzzy aggregation
([`GradedHV`](@ref), [`GradedBipolarHV`](@ref)) or phasor addition ([`FHRR`](@ref)).

!!! warning "Bundling is m-way, not pairwise"
    `bundle` combines **all** its inputs in one go, and for most types the result depends
    on how many there are -- a majority vote over `m` inputs, a `1/√m` rescaling, a
    renormalization. Bundling is therefore *not* associative for [`BinaryHV`](@ref),
    [`BipolarHV`](@ref), [`RealHV`](@ref) and [`FHRR`](@ref) (it is for
    [`TernaryHV`](@ref) and the graded types, whose rules happen to be associative).

    Three spellings do the right thing, because each reaches `bundle` with every input at
    once: `bundle(hvs)`, `sum(hvs)`, and chained `x + y + z` (Julia parses a chain of `+`
    as a single variadic call).

    Folding pairwise does **not**: `(x + y) + z`, `reduce(+, hvs)` and `foldl(+, hvs)`
    bundle a bundle. The result is heavily biased towards the last inputs and loses the
    defining property that a bundle is equally similar to each of its parts.

# See also

[`bind`](@ref), [`similarity`](@ref)
"""
function bundle(hdvs; kwargs...)
    hv = first(hdvs)
    r = empty_vector(hv)
    return bundle(hv, hdvs, r; kwargs...)
end

Base.:+(u::HV, v::AbstractArray...) where {HV <: AbstractHV} = bundle((u, v...))

# `sum` on a collection of hypervectors means bundling them, all at once. Without this
# method Base would fold pairwise with `+`, which bundles a bundle and biases the result
# towards the last elements (see the warning on `bundle`).
Base.sum(hvs::AbstractVector{<:AbstractHV}) = bundle(hvs)
Base.sum(hvs::Tuple{Vararg{AbstractHV}}) = bundle(hvs)

# BINDING
# -------
"""
    bind(hv1, hv2)
    bind(hvs::AbstractVector{<:AbstractHV})

Bind (associate) hypervectors into a single hypervector that is dissimilar to its
inputs while preserving distances. Overloaded as the `*` operator and inverted by
[`unbind`](@ref) (`/`) — except for [`RealHV`](@ref), whose binding is not exactly
invertible.

The binding rule depends on the hypervector type: XOR of the stored bits, which is
self-inverse ([`BinaryHV`](@ref), [`BipolarHV`](@ref)), elementwise multiplication
([`TernaryHV`](@ref), [`RealHV`](@ref)), fuzzy XOR ([`GradedHV`](@ref),
[`GradedBipolarHV`](@ref)) or complex multiplication ([`FHRR`](@ref)).

# See also

[`bundle`](@ref), [`unbind`](@ref), [`similarity`](@ref)
"""
Base.bind(hv1::HV, hv2::HV) where {HV <: AbstractHV} = HV(hv1.v .* hv2.v)  # default
Base.bind(hv1::BinaryHV, hv2::BinaryHV) = BinaryHV(hv1.v .⊻ hv2.v)
Base.bind(hv1::BipolarHV, hv2::BipolarHV) = BipolarHV(hv1.v .⊻ hv2.v)
Base.bind(hv1::TernaryHV, hv2::TernaryHV) = (v = hv1.v .* hv2.v; TernaryHV{eltype(v)}(v))  # inner: operands may hold accumulated counts
Base.bind(hv1::RealHV, hv2::RealHV) = RealHV(hv1.v .* hv2.v, hv1.distr)
Base.bind(hv1::GradedHV, hv2::GradedHV) = GradedHV(fuzzy_xor.(hv1.v, hv2.v), hv1.distr)
Base.bind(hv1::GradedBipolarHV, hv2::GradedBipolarHV) = GradedBipolarHV(fuzzy_xor_bipol.(hv1.v, hv2.v), hv1.distr)
Base.bind(hv1::FHRR, hv2::FHRR) = FHRR(hv1.v .* hv2.v)
Base.:*(hv1::HV, hv2::HV) where {HV <: AbstractHV} = bind(hv1, hv2)
Base.bind(hvs::AbstractVector{HV}) where {HV <: AbstractHV} = prod(hvs)


"""
    unbind(hv1, hv2)

Unbind `hv2` from `hv1`, inverting [`bind`](@ref): `unbind(bind(x, y), y)` recovers
`x`. Overloaded as the `/` operator.

For the XOR- and multiplication-based types ([`BinaryHV`](@ref), [`BipolarHV`](@ref),
[`TernaryHV`](@ref)) binding is self-inverse, so `unbind` is simply `bind`; the same
fallback gives approximate fuzzy unbinding for [`GradedHV`](@ref) and
[`GradedBipolarHV`](@ref). [`FHRR`](@ref) unbinds exactly via elementwise complex
division.

!!! warning
    Real-valued MAP binding is not exactly invertible, so `unbind` **throws** for
    [`RealHV`](@ref). Recover bound information with [`similarity`](@ref) against
    candidate hypervectors, or use [`FHRR`](@ref) or [`BipolarHV`](@ref) if you
    need exact unbinding.

# See also

[`bind`](@ref), [`bundle`](@ref), [`similarity`](@ref)
"""
unbind(hv1::HV, hv2::HV) where {HV <: AbstractHV} = bind(hv1, hv2)
unbind(hv1::FHRR, hv2::FHRR) = FHRR(hv1.v ./ hv2.v)
unbind(::RealHV, ::RealHV) = throw(
    ArgumentError(
        "real-valued MAP binding is not exactly invertible; recover bound " *
            "information with `similarity` against candidate hypervectors, or use " *
            "`FHRR` or `BipolarHV` if you need exact unbinding"
    )
)
Base.:/(hv1::HV, hv2::HV) where {HV <: AbstractHV} = unbind(hv1, hv2)


# SHIFTING
# --------

# Shifting / Permutation
"""
    shift!(hv::AbstractHV, k::Int)

Permutes hypervector in-place by a specified number of shifts.

This operations is used to assign an order to hypervectors.
"""
shift!(hv::AbstractHV, k = 1) = (circshift!(hv.v, k); hv)


"""
    shift(hv::AbstractHV, k::Int)

Permutes hypervector in-place by a specified number of shifts.

This operations is used to assign an order to hypervectors.
"""
function shift(hv::AbstractHV, k = 1)
    r = similar(hv)
    r.v .= circshift(hv.v, k)
    return r
end

function shift!(hv::V, k = 1) where {V <: Union{BinaryHV, BipolarHV}}
    v = similar(hv.v)  # empty bitvector
    hv.v .= circshift!(v, hv.v, k)
    return hv
end

function shift(hv::V, k = 1) where {V <: Union{BinaryHV, BipolarHV}}
    v = similar(hv.v)  # empty bitvector
    return V(circshift!(v, hv.v, k))
end

"""
    ρ(hv::AbstractHV, k::Int = 1)

Alias of [`shift`](@ref).
"""
ρ(hv::AbstractHV, k = 1) = shift(hv, k)

"""
    ρ!(hv::AbstractHV, k::Int = 1)

Alias of [`shift!`](@ref).
"""
ρ!(hv::AbstractHV, k = 1) = shift!(hv, k)


# Comparison
# Hypervectors of DIFFERENT types are never equal, even when their element
# values coincide numerically (`true == 1` would otherwise make an all-true
# BinaryHV equal an all-+1 BipolarHV, whose stored bits are the exact
# opposite). Within one type, equality compares storage — a bijection to the
# elements for every type — and TernaryHV{Int8} == TernaryHV{Int64} etc. still
# compare by value via the family methods. Comparisons against plain
# AbstractVectors keep Base's elementwise semantics, and hashing stays on the
# AbstractArray element-based fallback so that `isequal(hv, v::Vector)` implies
# equal hashes (a type-salted hash would break that contract).
Base.:(==)(::AbstractHV, ::AbstractHV) = false
Base.isequal(::AbstractHV, ::AbstractHV) = false
Base.:(==)(u::HV, v::HV) where {HV <: AbstractHV} = u.v == v.v
Base.isequal(u::HV, v::HV) where {HV <: AbstractHV} = isequal(u.v, v.v)
for T in (:TernaryHV, :RealHV, :GradedHV, :GradedBipolarHV, :FHRR)
    @eval Base.:(==)(u::$T, v::$T) = u.v == v.v
    @eval Base.isequal(u::$T, v::$T) = isequal(u.v, v.v)
end

"""
    Base.isapprox(u::AbstractHV, v::AbstractHV, atol=length(u)/100, ptol=0.01)

Measures when two hypervectors are similar (have more elements in common than expected
by chance).

One can specify either:
- `atol=N/100` number of matches more than due to chance needed for being assumed similar
- `ptol=0.01` threshold for seeing that many matches due to chance
"""
function Base.isapprox(u::T, v::T; atol = length(u) / 100, ptol = 0.01) where {T <: Union{BinaryHV, BipolarHV}}
    @assert length(u) == length(v) "Vectors have to be of equal length"
    N = length(u)
    missmatches = sum(ui != vi for (ui, vi) in zip(u, v))
    matches = N - missmatches
    # probability of seeing fewer mismatches due to chance
    pval = cdf(Binomial(N, 0.5), missmatches)
    return pval < ptol || matches - N / 2 > atol
end

"""
    Base.isapprox(u::AbstractHV, v::AbstractHV, atol=length(u)/100, ptol=0.01)

Measures when two hypervectors are similar (have more elements in common than expected
by chance) using the Hamming distance. Uses a bootstrap to construct a null distribution.

One can specify either:
- `ptol=1e-10` threshold for seeing that many matches due to chance
- `N_bootstrap=200` number of samples for bootstrapping
"""
function Base.isapprox(u::T, v::T; ptol = 1.0e-10, N_bootstrap = 500) where {T <: AbstractHV}
    @assert length(u) == length(v) "Vectors have to be of equal length"
    N = length(u)
    # bootstrap to find the zero distr
    B = [abs(rand(u) - rand(v)) for _ in 1:N_bootstrap]
    Bmean = N * mean(B)
    Bstd = sqrt(N) * std(B)
    # Hamming distance
    d = sum(abs(ui - vi) for (ui, vi) in zip(u, v))
    # probability of seeing fewer mismatches due to chance
    pval = cdf(Normal(Bmean, Bstd), d)
    return pval < ptol
end


# Perturbation
function randbv(n::Int, m::Int; rng::AbstractRNG = Random.default_rng())
    v = falses(n)
    v[1:m] .= true
    return shuffle!(rng, v)
end

function randbv(n::Int, p::Number; rng::AbstractRNG = Random.default_rng())
    @assert 0 ≤ p ≤ 1 "p should be a valid probability"
    return randbv(n, round(Int, p * n); rng = rng)
end

function randbv(n::Int, I; rng::AbstractRNG = Random.default_rng())
    v = falses(n)
    v[I] .= true
    return v
end

function perturbate!(::Type{HVByteVec}, hv::HV, I, dist = eldist(hv); rng::AbstractRNG = Random.default_rng()) where {HV <: AbstractHV}
    hv.v[I] .= rand(rng, dist, length(I))
    return hv
end

function perturbate!(::Type{HVByteVec}, hv::HV, M::BitVector, dist = eldist(hv); rng::AbstractRNG = Random.default_rng()) where {HV <: AbstractHV}
    hv.v[M] .= rand(rng, dist, sum(M))
    return hv
end

function perturbate!(::Type{HVByteVec}, hv::HV, p::Number, args...; rng::AbstractRNG = Random.default_rng()) where {HV <: AbstractHV}
    return perturbate!(hv, randbv(length(hv), p; rng = rng), args...; rng = rng)
end

# FHRR elements live on the complex unit circle, so perturbation resamples the
# phase at the selected positions instead of drawing from `eldist` (which would
# produce invalid, non-unit-modulus elements).
function perturbate!(::Type{HVByteVec}, hv::FHRR{Complex{R}}, I::AbstractVector{<:Integer}; rng::AbstractRNG = Random.default_rng()) where {R}
    hv.v[I] .= exp.(2π * im .* rand(rng, R, length(I)))
    return hv
end

function perturbate!(::Type{HVByteVec}, hv::FHRR{Complex{R}}, M::BitVector; rng::AbstractRNG = Random.default_rng()) where {R}
    hv.v[M] .= exp.(2π * im .* rand(rng, R, sum(M)))
    return hv
end

function perturbate!(::Type{HVBitVec}, hv::AbstractHV, binargs; rng::AbstractRNG = Random.default_rng())
    n = length(hv)
    M = randbv(n, binargs; rng = rng)
    hv.v .⊻= M
    return hv
end

"""
    perturbate!(hv::AbstractHV, args...; rng::AbstractRNG = Random.GLOBAL_RNG)

Perturbate hypervectors by randomly flipping values.

Refer to [`perturbate`](@ref) for example use cases.
"""
perturbate!(hv, args...; rng::AbstractRNG = Random.default_rng()) = perturbate!(vectype(hv), hv, args...; rng = rng)

"""
    perturbate(hv::AbstractHV, args...; rng::AbstractRNG = Random.GLOBAL_RNG)

Perturbate hypervectors by randomly flipping values.

# Examples

```julia-repl
julia> v = BinaryHV(ones(10))
10-element BinaryHV with 10 true and 0 false:
 1
 1
 1
 1
 1
 1
 1
 1
 1
 1

julia> perturbate(v, 1)
10-element BinaryHV with 9 true and 1 false:
 1
 1
 1
 1
 1
 0
 1
 1
 1
 1

julia> perturbate(v, 0.5)
10-element BinaryHV with 5 true and 5 false:
 0
 1
 1
 0
 1
 0
 0
 1
 0
 1

julia> perturbate(v, [1,2,3])
10-element BinaryHV with 7 true and 3 false:
 0
 0
 0
 1
 1
 1
 1
 1
 1
 1
```
"""
perturbate(hv::AbstractHV, args...; rng::AbstractRNG = Random.default_rng(), kwargs...) = perturbate!(copy(hv), args...; rng = rng, kwargs...)

# OTHER
# -----
Base.:^(hv::FHRR, x::Number) = FHRR(hv.v .^ x)
